// -*-C++-*-

/* Purpose: NCO/ncap2 script to calculate hyperpectral indices from the hyperspectral reflectances 
   ( this is the input variable rfl_img(wavelenth,y,x) ) 
   by default the averages of the required reflectances is calculated. (these have the postfix suffix "_avg" )
   These averages are then used to calculate the average of the indices. 


    
   set the following flags on the ncap2 command line 
   flg_dbg - if set then print debug info (default 0) 
  
   flg_abr - if set then calculate the averages before the ratio step  (default 0)

   flg_rba - if set then calculate the ratio (pixels) before the average step  (default 1)
  
   flg_mss - set the _FillValue 1.0e36 to all values in selected reflectances where value <=0.0f (default 1)

   usage examples 
   ncap2 -v -O  -S hyperspectral_indices_make.nco   in.nc out.nc                                         ( use default flags)     
   ncap2 -v -O -s 'flg_dbg=1s;flg_rba=1s;flg_mss=1s;' -S hyperspectral_indices_make.nco   in.nc out.nc   ( set flags )


*/
   


if(!exists(flg_dbg))
   *flg_dbg=0s;  // [flg] Print debug info

if(!exists(flg_rba))
   *flg_rba=1s;  // [flg] (ratio before average)  calculate average from _pxl ratio


if(!exists(flg_abr))
   *flg_abr=0s;  // [flg] (average before ratio) - calculate averages reflectances - then calculate ratios with these avergaes
                //       result is a scalar - (stored as an attribute)  



if(!exists(flg_mss)){
  *flg_mss=1s;  // [flg] add and set _FillValue to reflectance indices
  flt_mss=1.0e36f;
}



//required reflectances
@rfl_lst={"R445"s,"R450"s,"R470"s, "R500"s,"R512"s,"R531"s, "R540"s,"R550"s,"R570"s,"R586"s,"R590"s,"R600"s,"R650"s,"R670"s, "R680"s,"R690"s,"R700"s,"R705"s,"R710"s,"R720"s,"R740"s,"R750"s,"R760"s, "R780"s,"R790"s,"R800"s,"R900"s,"R970"s};  

// nb ONE-TO_ONE  correspondence with above list
@dbl_val_lst={ 4.45e-7d, 4.5e-7d, 4.7e-7d, 5.0e-7d, 5.12e-7, 5.31e-7, 5.4e-7, 5.5e-7, 5.7e-7,5.86e-7, 5.9e-7,6.0e-7,6.5e-7,6.7e-7, 6.8e-7, 6.9e-7, 7.0e-7, 7.05e-7, 7.1e-7,7.2e-7, 7.4e-7,7.5e-7,7.6e-7, 7.8e-7, 7.9e-7, 8.0e-7, 9.0e-7,9.7e-7};    

// use vpointers to calculate averages AFTER pixel ratios 
// 6-March 2017 - list not used right now but could be useful later 
@ind_lst={"NDVI"s,"SR"s, "OSAVI"s,"CHL"s,"msR705"s,"TCARI"s,"CarChap"s,"Car1Black"s,"Car2Black"s,"PRI570"s,"SIPI"s,"antGamon"s,"antGitelson"s,"CHLDela"s,"CI"s,"PRI586"s,"PRI512"s,"FRI1"s,"FRI2"s,"NDVI1"s,"RERI"s,"ZM"s,"REP"s,"NDRE"s,"TVI"s};



/* sanity check */
if( @rfl_lst.size() != @dbl_val_lst.size() )
{
  print("size difference between list @rfl_lst and @dbl_val_lst\n");
  exit(1);
}


/* extract the required reflectances - 
   nb min_coords() -extracts the index of the grid value nearest the required value 
   This step creates the vars iR445, iR450, iR470..iR970 */
{
  *idx=0;
  *sz=@rfl_lst.size();

  for(idx=0;idx<sz;idx++)
  {
    @R_nm=sprint(@rfl_lst(idx));
    @iR_nm=push("i",@R_nm);

    *@iR_nm=min_coords(wavelength,@dbl_val_lst(idx));
          
  }


}



/* sanity checks for required relectances -  make sure the are within resonable tolerance (1.0e-9) */
if( fabs(wavelength(iR445)-4.45e-7) > 0.01e-7d )  
{
  print("wavelength R445 not in wavelength coord\n");  
  exit(1);
}
if( fabs(wavelength(iR970)-9.70e-7) > 0.01e-7d )
{
  print("wavelength R970 not in wavelength coord\n");  
  exit(1);
}
if( fabs(wavelength(iR700)-7e-7) > 0.01e-7d )
{
  print("wavelength R700 not in wavelength coord\n");  
  exit(1);
}



/* This block hyperslabs the reflectances and their averages - with or with _FillValue */
/* That is the vars R445, R450..R970  and  R445_avg, R450_avg..R970_avg */
{
 

  if(flg_dbg)
    print(@rfl_lst);

  sz=@rfl_lst.size();

  for(idx=0; idx<sz ; idx++)
  {
    @var_nm=sprint(@rfl_lst(idx));
    @var_inm=push("i",@var_nm);   
    @var_nm_avg=push(@var_nm,"_avg");

    /* create R* variables from hyperslab */
    *@var_nm=rfl_img(*@var_inm,:,:);
   

    
    /* set and add missing value */   
   if(flg_mss) 
   {
    where (*@var_nm <= 0.0f)
      *@var_nm=flt_mss;  

    *@var_nm.set_miss(flt_mss);  
   }

    /* create avg of hyperslabs */
   if(flg_abr)
    *@var_nm_avg=*@var_nm.avg();
  
    if(flg_dbg)
       print(@var_nm,"%s\n");
 
  } 

}


NDVI_pxl = ( R900 -R680) / (R900+R680 );
NDVI@long_name="Normalized Difference Vegetation Index";
NDVI@standard_name="normalized_difference_vegetation_index";
NDVI@description="Normalized Difference Vegetation Index | (R900-R680)/(R900+R680) | Rouse et al. (1973)";
NDVI@notes="Normalized Difference Vegetation Index | NDVI";
NDVI@units="ratio";
NDV@label="Reflectance Index";
NDVI@type="trait";
if(flg_abr){
  NDVI_abr = ( R900_avg -R680_avg) / (R900_avg+R680_avg );
  NDVI_abr@long_name=NDVI@long_name;
 }

SR_pxl=R900 / R680;
SR@long_name="Simple ratio";
SR@standard_name="simple_ratio";
SR@description="Simple ratio | R900 / R680 | Rouse et al. (1973)";
SR@notes="Simple ratio | SR";
SR@units="ratio";
SR@label="Reflectance Index";
SR@type="trait";
if(flg_abr){
  SR_abr=R900_avg / R680_avg;
  SR_abr@long_name=SR@long_name;
}

OSAVI_pxl=1.16f*( (R800-R670) ) /  ( R800+R670+0.16f);
OSAVI@long_name="Optimized Soil-Adjusted Vegetation index";
OSAVI@standard_name="optimized_soil-adjusted_vegetation_index";
OSAVI@description="Optimized Soil-Adjusted Vegetation index | 1.16f*( (R800-R670) ) /  ( R800+R670+0.16f) | Rondeaux et al. (1996)";
OSAVI@notes="Optimized Soil-Adjusted Vegetation index | OSAVI";
OSAVI@units="ratio";
OSAVI@label="Reflectance Index";
OSAVI@type="trait";
if(flg_abr){
  OSAVI_abr=1.16f*( (R800_avg-R670_avg) ) /  ( R800_avg+R670_avg+0.16f);
  OSAVI_abr@long_name=OSAVI@long_name;
}


WI_pxl=R900-R970;
WI@long_name="water index";
WI@standard_name="water_index";
WI@description="water index | R900-R970 | Penuelas. et al. (1993)";
WI@notes="water index | WI";
WI@units="ratio";
WI@label="Reflectance Index";
WI@type="trait";
if(flg_abr){
  WI_abr=R900_avg-R970_avg;
  WI_abr@long_name=WI@long_name;
}

CHL_pxl=R750 / R550;
CHL@long_name="Chlorophyll index";
CHL@standard_name="chlorophyll_index";
CHL@description="Chlorophyll index | R750 / R550 | Gitelson and Merzlyak (1994)";
CHL@notes="Chlorophyll index | CHL";
CHL@units="ratio";
CHL@label="Reflectance Index";
CHL@type="trait";
if(flg_abr){
  CHL_abr=R750_avg / R550_avg;
  CHL_abr@long_name=CHL@long_name;
}



msR705_pxl= (R750-R445) / (R705-R445);
msR705@long_name="Modified simple ratio 705";
msR705@standard_name="modified_simple_ratio_705";
msR705@description="Modified simple ratio 705 | (R750-R445) / (R705-R445) | Sims and Gamon (2002)";
msR705@notes="Modified simple ratio 705 | msR705";
msR705@units="ratio";
msR705@label="Reflectance Index";
msR705@type="trait";
if(flg_abr){
  msR705_abr= (R750_avg-R445_avg) / (R705_avg-R445_avg);
  msR705_abr@long_name=msR705@long_name;
}

TCARI_pxl=3.0f*(  (R700-R670)-0.2f * (R700-R550) * (R700/R670)  );
TCARI@long_name="Transformed chlorophyll absorption in reflectance index";
TCARI@standard_name="transformed_chlorophyll_absorption_in_reflectance_index";
TCARI@description="Transformed chlorophyll absorption in reflectance index | 3.0f*(  (R700-R670)-0.2f * (R700-R550) * (R700/R670)  ) | Haboudane et al. (2002)";
TCARI@notes="Transformed chlorophyll absorption in reflectance index | TCARI";
TCARI@units="ratio";
TCARI@label="Reflectance Index";
TCARI@type="trait";
if(flg_abr){
  TCARI_abr=3.0f*(  (R700_avg-R670_avg)-0.2f * (R700_avg-R550_avg) * (R700_avg/R670_avg)  );
  TCARI_abr@long_name=TCARI@long_name;
}


CarChap_pxl=R760/R500;
CarChap@long_name="Carotenoid index (Chappelle)";
CarChap@standard_name="carotenoid_index_(chappelle)";
CarChap@description="Carotenoid index (Chappelle) | R760/R500 | Chappelle et al. (1992)";
CarChap@notes="Carotenoid index (Chappelle) | CarChap";
CarChap@label="Reflectance Index";
CarChap@type="trait";
if(flg_abr){
  CarChap_abr=R760_avg/R500_avg;
  CarChap_abr@long_name=CarChap@long_name;
}

Car1Black_pxl=R800/R470;
Car1Black@long_name="Carotenoid index (BlackBurn)";
Car1Black@standard_name="carotenoid_index_(blackburn)";
Car1Black@description="Carotenoid index (BlackBurn) | R800/R470 | Blackburn (1998)";
Car1Black@notes="Carotenoid index (BlackBurn) | Car1Black";
Car1Black@units="ratio";
Car1Black@label="Reflectance Index";
Car1Black@type="trait";
if(flg_abr){
  Car1Black_abr=R800_avg/R470_avg;
  Car1Black_abr@long_name=Car1Black@long_name;
 }


Car2Black_pxl= ( R800 - R470 ) / (R800 + R470);
Car2Black@long_name="Carotenoid index 2 (BlackBurn)";
Car2Black@standard_name="carotenoid_index_2_(blackBurn)";
Car2Black@description="Carotenoid index 2 (BlackBurn) | ( R800 - R470 ) / (R800 + R470) | Blackburn (1998)";
Car2Black@notes="Carotenoid index 2 (BlackBurn) | Car2Black";
Car2Black@units="ratio";
Car2Black@label="Reflectance Index";
Car2Black@type="trait";
if(flg_abr){
  Car2Black_abr= ( R800_avg - R470_avg ) / (R800_avg + R470_avg);
  Car2Black_abr@long_name=Car2Black@long_name;
}


PRI570_pxl = (R531 - R570) / (R531+R570);
PRI570@long_name="Photochemical reflectance index (570)";
PRI570@standard_name="photochemical_reflectance_index_(570)";
PRI570@description="Photochemical reflectance index (570) | (R531 - R570) / (R531+R570) | Gamon et al. (1992)";
PRI570@notes="Photochemical reflectance index (570) | PRI570";
PRI570@units="ratio";
PRI570@label="Reflectance Index";
PRI570@type="trait";
if(flg_abr){
  PRI570_abr = (R531_avg - R570_avg) / (R531_avg+R570_avg);
  PRI570_abr@long_name=PRI570@long_name;
}

SIPI_pxl =( R800 - R450) / (R800 + R650);
SIPI@long_name="Structure intensive pigment index";
SIPI@standard_name="structure_intensive_pigment_index";
SIPI@description="Structure intensive pigment index | ( R800 - R450) / (R800 + R650) | Penuelas. et al. (1995)";
SIPI@notes="Structure intensive pigment index | SIPI";
SIPI@units="ratio";
SIPI@label="Reflectance Index";
SIPI@type="trait";
if(flg_abr){
  SIPI_abr =( R800_avg - R450_avg) / (R800_avg + R650_avg);
  SIPI_abr@long_name=SIPI@long_name;
}


antGamon_pxl=R650/R550;
antGamon@long_name="Anthocyanin (Gamon)";
antGamon@standard_name="anthocyanin_(gamon)";
antGamon@description="Anthocyanin (Gamon) | R650/R550 | Gamon and Surfus (1999)";
antGamon@notes="Anthocyanin (Gamon) | antGamon";
antGamon@units="ratio";
antGamon@label="Reflectance Index";
antGamon@type="trait";

if(flg_abr){
  antGamon_abr=R650_avg/R550_avg;
  antGamon_abr@long_name=antGamon@long_name;
}


antGitelson_pxl=( 1.0f/R550 - 1.0f/R700)*R780;
antGitelson@long_name="Anthocyanin (Gitelson)";
antGitelson@standard_name="anthocyanin_(gitelson)";
antGitelson@description="Anthocyanin (Gitelson) | ( 1.0f/R550 - 1.0f/R700)*R780 | Gitelson et al.(2003,2006)";
antGitelson@notes="Anthocyanin (Gitelson) | antGitelson";
antGitelson@units="ratio";
antGitelson@label="Reflectance Index";
antGitelson@type="trait";
if(flg_abr){
  antGitelson_abr=( 1.0f/R550_avg - 1.0f/R700_avg)*R780_avg;
  antGitelson_abr@long_name=antGitelson@long_name;
}

CHLDela_pxl=( R540-R590 ) / ( R540 + R590);
CHLDela@long_name="Chlorophyll content";
CHLDela@standard_name="chlorophyll_content";
CHLDela@description="Chlorophyll content | ( R540-R590 ) / ( R540 + R590) | Delaieux et al. (2014)";
CHLDela@notes="Chlorophyll content | CHLDela";
CHLDela@units="ratio";
CHLDela@label="Reflectance Index";
CHLDela@type="trait";
if(flg_abr){
  CHLDela_abr=( R540_avg-R590_avg ) / ( R540_avg + R590_avg);
  CHLDela_abr@long_name=CHLDela@long_name;
 }


CI_pxl=( R750-R705 ) / ( R750 + R705);
CI@long_name="Chlorophyll index";
CI@standard_name="chlorophyll_index";
CI@description="Chlorophyll index | ( R750-R705 ) / ( R750 + R705) | Gitelson and Merzlyak (1994)";
CI@notes="Chlorophyll index | CI";
CI@units="ratio";
CI@label="Reflectance Index";
CI@type="trait";
if(flg_abr){
  CI_abr=( R750_avg-R705_avg ) / ( R750_avg + R705_avg);
  CI_abr@long_name=CI@long_name;
}


PRI586_pxl=( R531-R586) / ( R531 + R586);
PRI586@long_name="Photochemical reflectance index (586)";
PRI586@standard_name="photochemical_reflectance_index_(586)";
PRI586@description="Photochemical reflectance index (586) | ( R531-R586) / ( R531 + R586) | Panigada et al. (2014)";
PRI586@notes="Photochemical reflectance index (586) | PRI586";
PRI586@units="ratio";
PRI586@label="Reflectance Index";
PRI586@type="trait";
if(flg_abr){
  PRI586_abr=( R531_avg-R586_avg) / ( R531_avg + R586_avg);
  PRI586_abr@long_name=PRI586@long_name;
}


PRI512_pxl=( R531-R512) / ( R531 + R512);
PRI512@long_name="Photochemical reflectance index (512)";
PRI512@standard_name="photochemical_reflectance_index_(512)";
PRI512@description="Photochemical reflectance index (512) | ( R531-R512) / ( R531 + R512) | HernÃ¡ndez-Clemente et al. (2011)";
PRI512@notes="Photochemical reflectance index (512) | PRI512";
PRI512@units="ratio";
PRI512@label="Reflectance Index";
PRI512@type="trait";
if(flg_abr){
  PRI512_abr=( R531_avg-R512_avg) / ( R531_avg + R512_avg);
  PRI512_abr@long_name=PRI512@long_name;
}

FRI1_pxl=R690 / R600;
FRI1@long_name="Fluorescence ratio index1";
FRI1@standard_name="fluorescence_ratio_index1";
FRI1@description="Fluorescence ratio index1 | R690 / R600 | Dobrowski et al. (2005)";
FRI1@notes="Fluorescence ratio index1 | FRI1";
FRI1@units="ratio";
FRI1@label="Reflectance Index";
FRI1@type="trait";
if(flg_abr){
  FRI1_abr=R690_avg / R600_avg;
  FRI1_abr@long_name=FRI1@long_name;
}


FRI2_pxl=R740 / R800;
FRI2@long_name="Fluorescence ratio indices 2";
FRI2@standard_name="fluorescence_ratio_indices_2";
FRI2@description="Fluorescence ratio indices 2 | R740 / R800 | Dobrowski et al. (2005)";
FRI2@notes="Fluorescence ratio indices 2 | FRI2";
FRI2@units="ratio";
FRI2@label="Reflectance Index";
FRI2@type="trait";
if(flg_abr){
  FRI2_abr=R740_avg / R800_avg;
  FRI2_abr@long_name=FRI2@long_name;
}

NDVI1_pxl=(R800-R670)/ (R800+R670);
NDVI1@long_name="Normalized Difference Vegetation Index1";
NDVI1@standard_name="normalized_difference_vegetation_index1";
NDVI1@description="Normalized Difference Vegetation Index1 | (R800-R670)/ (R800+R670) | Rouse et al. (1973)";
NDVI1@notes="Normalized Difference Vegetation Index1 | NDVI1";
NDVI1@units="ratio";
NDVI1@label="Reflectance Index";
NDVI1@type="trait";
if(flg_abr){
  NDVI1_abr=(R800_avg-R670_avg)/ (R800_avg+R670_avg);
  NDVI1_abr@long_name=NDVI1@long_name;
}

RDVI_pxl=NDVI1_pxl*0.5f;
RDVI@long_name="Renormalized Difference Vegetation Index";
RDVI@standard_name="renormalized_difference_vegetation_index";
RDVI@description="Renormalized Difference Vegetation Index | NDVI1*0.5f | Rougean and Breon (1995)";
RDVI@notes="Renormalized Difference Vegetation Index | RDVI";
RDVI@units="ratio";
RDVI@label="Reflectance Index";
RDVI@type="trait";
if(flg_abr){
  RDVI_abr=NDVI1_abr*0.5f;
  RDVI_abr@long_name=RDVI@long_name;
}

RERI_pxl=R700/R670;
RERI@long_name="Red edge ratio index";
RERI@standard_name="red_edge_ratio_index";
RERI@description="Red edge ratio index | R700/R670 | Part of TCARI index";
RERI@notes="Red edge ratio index | RERI";
RERI@units="ratio";
RERI@label="Reflectance Index";
RERI@type="trait";
if(flg_abr){
  RERI_abr=R700_avg/R670_avg;
  RERI_abr@long_name=RERI@long_name;
 }

ZM_pxl= R750 / R710;
ZM@long_name="Red edge";
ZM@standard_name="red_edge";
ZM@description="Red edge | R750 / R710 | Zarco-Tejada et al. (2001)";
ZM@notes="Red edge | ZM";
ZM@units="ratio";
ZM@label="Reflectance Index";
ZM@type="trait";
if(flg_abr){
  ZM_abr= R750_avg / R710_avg;
  ZM_abr@long_name=ZM@long_name;
}

REP_pxl=700.0f +40.0f*(((R670+R780)/2-R700) /(R740-R700));
REP@long_name="Red edge position";
REP@standard_name="red_edge_position";
REP@description="Red edge position | 700.0f +40.0f*(((R670+R780)/2-R700) /(R740-R700)) | Guyot and Baret, 1988";
REP@notes="Red edge position | REP";
REP@units="ratio";
REP@label="Reflectance Index";
REP@type="trait";
if(flg_abr){
  REP_abr=700.0f +40.0f*(((R670_avg+R780_avg)/2-R700_avg) /(R740_avg-R700_avg));
  REP_abr@long_name=REP@long_name;
 }

NDRE_pxl=(R790-R720)/(R790+R720);
NDRE@long_name="Normalized difference vegetation index";
NDRE@standard_name="normalized_difference_vegetation_index";
NDRE@description="Normalized difference vegetation index | (R790-R720)/(R790+R720) | Barnes et al. (2000)";
NDRE@notes="Normalized differenc vegetation index | NDRE";
NDRE@units="ratio";
NDRE@label="Reflectance Index";
NDRE@type="trait";
if(flg_abr){
  NDRE_abr=(R790_avg-R720_avg)/(R790_avg+R720_avg);
  NDRE_abr@long_name=NDRE@long_name;
}


TVI_pxl=pow( 0.5f, (120.0f*(R750-R550)-200.0f*(R670-R550)));
TVI@long_name="Triangular Vegetation Index";
TVI@standard_name="triangular_vegetation_index";
TVI@description="Triangular Vegetation Index | 0.5f**(120.0f*(R750-R550)-200.0f*(R670-R550))) | Haboudaneet al. (2004)";
TVI@notes="Triangular Vegetation Index | TVI";
TVI@units="ratio";
TVI@label="Reflectance Index";
TVI@type="trait";
if(flg_abr){
  TVI_abr=pow( 0.5f, (120.0f*(R750_avg-R550_avg)-200.0f*(R670_avg-R550_avg)));
  TVI_abr@long_name=TVI@long_name;
}

/* create avg from pixel formula 
 this creates the vars NDVI, SR, OSAVI by taking average of the vars NDVI_pxl, SR_pxl, OSAVI_pxl  
 and also min,max that is NDVI@min, NDVI@max, SR@min, SR@max, OSAVI@min, OSAVI@max ...,*/

if(flg_rba){ 
  /* nb sz and idx are already RAM vars */

  @vars_abr=get_vars_out(".*_pxl");
  
  
  if(flg_dbg)
    print(@vars_abr);
  

  sz=@vars_abr.size();

 
  for(idx=0;idx<sz;idx++)   
  {
    @name_pxl=sprint(@vars_abr(idx));
     // remove the _abr suffix with a negative index -yah
     @name=@name_pxl(0:-5);
     @name_min=push(@name,"@min");
     @name_max=push(@name,"@max");

     if(exists(*@name_pxl))
     {
       // average of ratio 
       *@name=*@name_pxl.avg();
       // min
       *@name_min=*@name_pxl.min();    
       // max
       *@name_max=*@name_pxl.max();    

     }  
  }  
}  
